
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						DRUID TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//COPY MARKER FILE___________________________________________________________________
//
COPY ~scales_of_balance/misc/d5_druids.d5~ ~override~
//___________________________________________________________________________________

//UPDATE DRUID STRINGS_______________________________________________________________
// 
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	STRING_SET 9560 @5802
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	STRING_SET 9560 @5803
END

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
	STRING_SET 25314 @5804
	STRING_SET 25345 @5805
	STRING_SET 25376 @5806
	STRING_SET 25377 @5807
END
ACTION_IF ENGINE_IS ~tob bg2ee~ THEN BEGIN
	STRING_SET 25199 @5804
	STRING_SET 25173 @5805
	STRING_SET 25225 @5806
	STRING_SET 25227 @5807
END

ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	STRING_SET 37177 @5833 // or 10186, 13827, 13828
	STRING_SET 37176 @5836 // or 9560
	STRING_SET 37182 @5804 // or 25199
	STRING_SET 37184 @5805 // or 25173
	STRING_SET 37183 @5806 // or 25225
	STRING_SET 37186 @5807 // or 25227
END
//___________________________________________________________________________________

//EDIT DRUID KIT 2DA FILES___________________________________________________________
//
ACTION_IF NOT GAME_IS ~iwdee~ THEN BEGIN
	COPY_EXISTING ~clabdr01.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPCL611~ ~****      ~
		REPLACE_TEXTUALLY ~GA_SPCL612~ ~****      ~
		REPLACE_TEXTUALLY ~GA_SPCL613~ ~****      ~
		APPEND_FILE ~scales_of_balance/druid/drushap.txt~
		BUT_ONLY
END
ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	COPY_EXISTING ~clabdr01.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPCL112~ ~****      ~
		REPLACE_TEXTUALLY ~GA_SPCL113~ ~****      ~
		APPEND_FILE ~scales_of_balance/druid/iwdshap.txt~
		BUT_ONLY
END

COPY_EXISTING ~clabdr03.2da~ ~override~
	REPLACE_TEXTUALLY ~GA_SPCL643~ ~****      ~
	REPLACE_TEXTUALLY ~GA_SPCL644~ ~****      ~
	APPEND_FILE ~scales_of_balance/druid/lycshap.txt~
	BUT_ONLY
	
COPY_EXISTING ~KITLIST.2da~ ~override~
	SET_2DA_ENTRY 20 7 1 ~0x20000000 ~
	BUT_ONLY

COPY_EXISTING ~clabdr04.2da~ ~override~
	REPLACE_TEXTUALLY ~GA_SPCL611~ ~****      ~
	REPLACE_TEXTUALLY ~GA_SPCL612~ ~****      ~
	REPLACE_TEXTUALLY ~GA_SPCL613~ ~****      ~
	REPLACE_TEXTUALLY ~GA_SPCL632~ ~****      ~
	REPLACE_TEXTUALLY ~GA_SPCL633~ ~****      ~
	REPLACE_TEXTUALLY ~GA_SPCL634~ ~****      ~
	APPEND_FILE ~scales_of_balance/druid/aveshap.txt~
	BUT_ONLY

COPY_EXISTING ~luabbr.2da~ ~override~
	SET_2DA_ENTRY 44 1 1 ~Dr2~
	SET_2DA_ENTRY 45 1 1 ~Dr3~
	SET_2DA_ENTRY 46 1 1 ~Dr0~		
	BUT_ONLY

ACTION_IF NOT FILE_EXISTS_IN_GAME ~LUDR4.2da~ THEN BEGIN
	COPY_EXISTING ~LUDR0.2da~ ~override/ludr4.2da~
	END
	
ACTION_IF FILE_EXISTS_IN_GAME ~TG#BLON.spl~ THEN BEGIN // check for Refinements
	COPY_EXISTING ~LUDR0.2da~ ~override~
		SET_2DA_ENTRY 8 1 1 ~GA_d5_PSHAM ~
		SET_2DA_ENTRY 8 6 1 ~4~
		BUT_ONLY	
	COPY_EXISTING ~LUDR3.2da~ ~override~
		SET_2DA_ENTRY 8 1 1 ~GA_d5_PWW3 ~
		SET_2DA_ENTRY 8 6 1 ~4~
		BUT_ONLY	
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~TG#BLON.spl~ THEN BEGIN // check for Refinements
	COPY_EXISTING ~LUDR0.2da~ ~override/LUDR3.2da~
	COPY_EXISTING ~LUDR0.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPPR731 ~ ~GA_d5_PSHAM~
		REPLACE_TEXTUALLY ~GA_SPPR732 ~ ~GA_d5_PSHAM~
		REPLACE_TEXTUALLY ~99         22 ~ ~99         2  ~
		BUT_ONLY
	COPY_EXISTING ~LUDR3.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPPR731 ~ ~GA_d5_PWW3 ~
		REPLACE_TEXTUALLY ~GA_SPPR732 ~ ~GA_d5_PWW3 ~
		REPLACE_TEXTUALLY ~99         22 ~ ~99         2  ~
		BUT_ONLY
END
//__________________________________________________________________________________

//ELF DRUIDS________________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~CLSRCREQ.2da~ THEN BEGIN
	ACTION_IF GAME_IS ~bgee bg2ee~ THEN BEGIN
		COPY_EXISTING ~clsrcreq.2da~ ~override~
			SET_2DA_ENTRY 38 2 8 ~1~
	END		
	ACTION_IF ENGINE_IS ~tob~ THEN BEGIN
		COPY_EXISTING ~clsrcreq.2da~ ~override~
			SET_2DA_ENTRY 7 2 7 ~1~
	END
END
//__________________________________________________________________________________

//SOUTHERN DRUID____________________________________________________________________
//
ACTION_IF GAME_IS ~iwdee~ THEN BEGIN

ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN

ADD_KIT ~D5DRUID~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5DRUID       0 0 0 1 1 1 0 1~
~D5DRUID       0 0 0 1 1 1 0 1 0 0 0 0 0 0 1 1 0 1 1 0 0 0 1 0 0 0 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   12  7~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   17  0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   15  0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5DRUID           0  1  0  1  1  1  0  1  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5DRUID           1  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~scales_of_balance/druid/d5_druid.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_D_H    K_D_D   K_D_G   K_D_E   K_D_HE   K_D_HL   K_D_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 11~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Dr0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5834
SAY @5835
SAY @5832
//__________________________________________________________________________________

END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN

ADD_KIT ~D5DRUID~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5DRUID       0 0 0 3 3 3 0 3~
~D5DRUID       0 0 0 3 3 3 0 3 0 0 0 0 0 0 0 3 0 3 3 0 3 0 3 0 0 3 0 3 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   12  7~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   17  0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5DRUID           0   0   0   0   15  0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5DRUID           0  1  0  1  1  1  0  1  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5DRUID           1  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~scales_of_balance/druid/d5_druid.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_D_H    K_D_D   K_D_G   K_D_E   K_D_HE   K_D_HL   K_D_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 11~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Dr0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5834
SAY @5835
SAY @5832
//__________________________________________________________________________________

END

//EE KIT EXTRAS_____________________________________________________________________
LAF fl#add_kit_ee
	INT_VAR
		briefdesc = RESOLVE_STR_REF (@5832)
	STR_VAR
		kit_name = D5DRUID
END
//____________________________________________________________________________________

END

//COPY DRUID ABILITY FILES___________________________________________________________
//
COPY ~scales_of_balance/druid/d5_pwolf.spl~ ~override~
	SAY NAME1 @5808
	SAY UNIDENTIFIED_DESC @5808
COPY ~scales_of_balance/druid/d5_prat.spl~ ~override~
	SAY NAME1 @5839
	SAY UNIDENTIFIED_DESC @5839
COPY ~scales_of_balance/druid/d5_pdire.spl~ ~override~
	SAY NAME1 @5809
	SAY UNIDENTIFIED_DESC @5809
COPY ~scales_of_balance/druid/d5_pbear.spl~ ~override~
	SAY NAME1 @5810
	SAY UNIDENTIFIED_DESC @5810
COPY ~scales_of_balance/druid/d5_plion.spl~ ~override~
	SAY NAME1 @5811
	SAY UNIDENTIFIED_DESC @5811
COPY ~scales_of_balance/druid/d5_pspid.spl~ ~override~
	SAY NAME1 @5812
	SAY UNIDENTIFIED_DESC @5812
COPY ~scales_of_balance/druid/d5_pspi1.spl~ ~override~
	SAY NAME1 @5837
	SAY UNIDENTIFIED_DESC @5837
COPY ~scales_of_balance/druid/d5_pspi2.spl~ ~override~
	SAY NAME1 @5838
	SAY UNIDENTIFIED_DESC @5838
COPY ~scales_of_balance/druid/d5_pwyvn.spl~ ~override~
	SAY NAME1 @5813
	SAY UNIDENTIFIED_DESC @5813
COPY ~scales_of_balance/druid/d5_ptrol.spl~ ~override~
	SAY NAME1 @5814
	SAY UNIDENTIFIED_DESC @5814
COPY ~scales_of_balance/druid/d5_psham.spl~ ~override~
	SAY NAME1 @5815
	SAY UNIDENTIFIED_DESC @5816
COPY ~scales_of_balance/druid/d5_wolf.cre~ ~override~
COPY ~scales_of_balance/druid/d5_bear1.cre~ ~override~
COPY ~scales_of_balance/druid/d5_ratdr.cre~ ~override~
COPY ~scales_of_balance/druid/d5_lion1.cre~ ~override~
COPY ~scales_of_balance/druid/d5_spid1.cre~ ~override~
COPY ~scales_of_balance/druid/d5_spid2.cre~ ~override~
COPY ~scales_of_balance/druid/d5_wyv2.cre~ ~override~
COPY ~scales_of_balance/druid/d5_trol3.cre~ ~override~
COPY ~scales_of_balance/druid/d5_sham.cre~ ~override~
COPY ~scales_of_balance/druid/d5_claw1.itm~ ~override~
COPY ~scales_of_balance/druid/d5_claw1.spl~ ~override~
COPY ~scales_of_balance/druid/d5_claw2.itm~ ~override~
COPY ~scales_of_balance/druid/d5_claw2.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clrat.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clrat.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clsha.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clsha.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clspi.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clspi.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clsp2.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clsp2.spl~ ~override~
COPY ~scales_of_balance/druid/d5_cltro.itm~ ~override~
COPY ~scales_of_balance/druid/d5_cltro.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clwyv.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clwyv.spl~ ~override~
COPY ~scales_of_balance/druid/d5_rat1.spl~ ~override~
	SAY NAME1 @5840
	SAY UNIDENTIFIED_DESC @5840
COPY ~scales_of_balance/druid/d5_plynf.spl~ ~override~
COPY ~scales_of_balance/druid/d5_pnf1.spl~ ~override~
COPY ~scales_of_balance/druid/d5_poly.spl~ ~override~
COPY ~scales_of_balance/druid/d5_pww1.spl~ ~override~
	SAY NAME1 @5817
	SAY UNIDENTIFIED_DESC @5817
COPY ~scales_of_balance/druid/d5_pww2.spl~ ~override~
	SAY NAME1 @5818
	SAY UNIDENTIFIED_DESC @5818
COPY ~scales_of_balance/druid/d5_pww3.spl~ ~override~
	SAY NAME1 @5819
	SAY UNIDENTIFIED_DESC @5820
COPY ~scales_of_balance/druid/d5_dire.cre~ ~override~
COPY ~scales_of_balance/druid/d5_ww01.cre~ ~override~
	SAY NAME1 @5821
COPY ~scales_of_balance/druid/d5_ww02.cre~ ~override~
	SAY NAME1 @5822
COPY ~scales_of_balance/druid/d5_ww03.cre~ ~override~
	SAY NAME1 @5823
COPY ~scales_of_balance/druid/d5_plion.bam~ ~override~
COPY ~scales_of_balance/druid/d5_pspid.2da~ ~override~
/*
COPY ~scales_of_balance/druid/d5_pele1.spl~ ~override~
	SAY NAME1 @5824
	SAY UNIDENTIFIED_DESC @5824
COPY ~scales_of_balance/druid/d5_pele2.spl~ ~override~
	SAY NAME1 @5824
	SAY UNIDENTIFIED_DESC @5824
COPY ~scales_of_balance/druid/d5_pelw1.spl~ ~override~
	SAY NAME1 @5825
	SAY UNIDENTIFIED_DESC @5825
COPY ~scales_of_balance/druid/d5_pelw2.spl~ ~override~
	SAY NAME1 @5825
	SAY UNIDENTIFIED_DESC @5825
COPY ~scales_of_balance/druid/d5_pela1.spl~ ~override~
	SAY NAME1 @5826
	SAY UNIDENTIFIED_DESC @5826
COPY ~scales_of_balance/druid/d5_pela2.spl~ ~override~
	SAY NAME1 @5826
	SAY UNIDENTIFIED_DESC @5826
COPY ~scales_of_balance/druid/d5_pelf1.spl~ ~override~
	SAY NAME1 @5827
	SAY UNIDENTIFIED_DESC @5827
COPY ~scales_of_balance/druid/d5_pelf2.spl~ ~override~
	SAY NAME1 @5827
	SAY UNIDENTIFIED_DESC @5827
COPY ~scales_of_balance/druid/d5_ele01.cre~ ~override~
	SAY NAME1 @5828
COPY ~scales_of_balance/druid/d5_ele02.cre~ ~override~
	SAY NAME1 @5828
COPY ~scales_of_balance/druid/d5_elw01.cre~ ~override~
	SAY NAME1 @5829
COPY ~scales_of_balance/druid/d5_elw02.cre~ ~override~
	SAY NAME1 @5829
COPY ~scales_of_balance/druid/d5_ela01.cre~ ~override~
	SAY NAME1 @5830
COPY ~scales_of_balance/druid/d5_ela02.cre~ ~override~
	SAY NAME1 @5830
COPY ~scales_of_balance/druid/d5_elf01.cre~ ~override~
	SAY NAME1 @5831
COPY ~scales_of_balance/druid/d5_elf02.cre~ ~override~
	SAY NAME1 @5831
COPY ~scales_of_balance/druid/d5_clela.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clela.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clele.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clele.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clelw.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clelw.spl~ ~override~
COPY ~scales_of_balance/druid/d5_clelf.itm~ ~override~
COPY ~scales_of_balance/druid/d5_clelf.spl~ ~override~
COPY ~scales_of_balance/druid/d5_aire.bam~ ~override~
*/
//___________________________________________________________________________________

//EDIT CERND'S ITEMS_________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~npclck.itm~ THEN BEGIN 
	COPY_EXISTING ~npclck.itm~ ~override~
				~npstaf.itm~ ~override~
		READ_BYTE 0x29 "avenger"
		WRITE_BYTE 0x29 ("%avenger%" BAND "0b01001000")
		BUT_ONLY
END
//___________________________________________________________________________________

//EDIT DRUID CRE FILES_______________________________________________________________
//
ACTION_IF GAME_IS ~tutu bgt bgee~ THEN BEGIN
	COPY_EXISTING ~jaheir.cre~ ~override~
				~jaheir2.cre~ ~override~
				~faldor.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		BUT_ONLY
	COPY_EXISTING ~jaheir4.cre~ ~override~
				~jaheir6.cre~ ~override~
				~faldor5.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_prat~ END
		BUT_ONLY
END

ACTION_IF GAME_IS ~bgt tob bg2ee~ THEN BEGIN
	COPY_EXISTING ~jaheir7.cre~ ~override~
				~jaheir8.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_prat~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_plion~ END
		BUT_ONLY
	COPY_EXISTING ~jaheir11.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_prat~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_plion~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pbear~ END
		BUT_ONLY
	COPY_EXISTING ~jaheir12.cre~ ~override~
				~jahei12b.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_prat~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_plion~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pbear~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwyvn~ END
		BUT_ONLY
	COPY_EXISTING ~jahei14.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_prat~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_plion~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pbear~ END
		BUT_ONLY

	COPY_EXISTING ~cernd10.cre~ ~override~
				~cernd12.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pww1~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pww1~ END
		BUT_ONLY
	COPY_EXISTING ~cernd13.cre~ ~override~
				~cernd13b.cre~ ~override~
				~cernd14.cre~ ~override~
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pwolf~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pww1~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pww1~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=171 target=1 timing=9 power=1 STR_VAR resource=~d5_pww2~ END
		BUT_ONLY
END
//__________________________________________________________________________________

//DRUID WEAPONS_____________________________________________________________________
//
DEFINE_PATCH_FUNCTION ~REMOVE_CLASS_LINE_FROM_USABILITIES~
  INT_VAR
    offset = 0
  STR_VAR
    desc = ~~
    unusable_regexp = ~~
    unusable_replacement = ~~
    class_regexp = ~~ //
    class_prefix = ~~ //
BEGIN
  INNER_PATCH_SAVE desc ~%desc%~ BEGIN
    REPLACE_TEXTUALLY ~%unusable_regexp%~ ~%unusable_replacement%~
  END
  PATCH_IF ((~%desc%~ STRING_CONTAINS_REGEXP ~^%unusable_replacement%~) == 0) BEGIN
    INNER_PATCH_SAVE usab_block ~%desc%~ BEGIN
      REPLACE_TEXTUALLY ~\(.*[%LNL%%MNL%%WNL%]\)*%unusable_replacement%~ ~~
    END
    INNER_PATCH_SAVE usab_block_new ~%usab_block%~ BEGIN
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%][- %TAB%]*$~ ~~
      REPLACE_TEXTUALLY ~^[- %TAB%]*~ ~%class_prefix%~
      REPLACE_TEXTUALLY ~%mage_regexp%~ ~~
    END
    INNER_PATCH_SAVE desc ~%desc%~ BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ~%usab_block%~ ~%usab_block_new%~
    END
    INNER_PATCH_SAVE compare ~%desc%~ BEGIN 
      REPLACE_TEXTUALLY ~^%unusable_replacement%.*[%LNL%%MNL%%WNL%]%class_prefix%[^- %TAB%]~ ~~
    END
    PATCH_IF (~%desc%~ STRING_EQUAL ~%compare%~) BEGIN
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]%unusable_replacement%.*~ ~~
      END
    END
    SAY_EVALUATED ~%offset%~ ~%desc%~
  END
END

COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ // copies all item files
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
		READ_BYTE  0x21 class 
		READ_BYTE  0x1f multi 
		READ_SHORT  ~0x1c~ type 
		PATCH_IF (type = 15) BEGIN // bows
			WRITE_BYTE 0x21 (class BAND 0b10111111) // makes usable by druids
			WRITE_BYTE 0x1f (multi BAND 0b11101111) // makes usable by fighter/druids
			PATCH_FOR_EACH offset IN
				~0x50~
				~0x54~
			BEGIN
				READ_LONG ~%offset%~ desc_strref
				PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
					READ_STRREF ~%offset%~ desc
					LAUNCH_PATCH_FUNCTION ~REMOVE_CLASS_LINE_FROM_USABILITIES~
						STR_VAR
						unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*\(Unusable By\|Not Usable By\).*~
						unusable_replacement = ~Not Usable By:~
						class_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%].*\bDruid.*~
						class_prefix = ~ ~
					END
				END
			END
		END
		PATCH_IF (type = 5) BEGIN // arrows
			WRITE_BYTE 0x21 (class BAND 0b10111111) // makes usable by druids
			WRITE_BYTE 0x1f (multi BAND 0b11101111) // makes usable by fighter/druids
			PATCH_FOR_EACH offset IN
				~0x50~
				~0x54~
			BEGIN
				READ_LONG ~%offset%~ desc_strref
				PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
					READ_STRREF ~%offset%~ desc
					LAUNCH_PATCH_FUNCTION ~REMOVE_CLASS_LINE_FROM_USABILITIES~
						STR_VAR
						unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*\(Unusable By\|Not Usable By\).*~
						unusable_replacement = ~Not Usable By:~
						class_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%].*\bDruid.*~
						class_prefix = ~ ~
					END
				END
			END
		END
	END
	BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ // copies all item files
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
		READ_BYTE  0x21 class
		READ_BYTE  0x1f multi
		READ_SHORT  ~0x1c~ type
		PATCH_IF (type = 23) BEGIN // flails
			WRITE_BYTE 0x21 (class BAND 0b10111111) // makes usable by druids
			WRITE_BYTE 0x1f (multi BAND 0b11101111) // makes usable by fighter/druids
			PATCH_FOR_EACH offset IN
				~0x50~
				~0x54~
			BEGIN
				READ_LONG ~%offset%~ desc_strref
				PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
					READ_STRREF ~%offset%~ desc
					LAUNCH_PATCH_FUNCTION ~REMOVE_CLASS_LINE_FROM_USABILITIES~
						STR_VAR
						unusable_regexp = EVALUATE_BUFFER ~^[ %TAB%]*\(Unusable By\|Not Usable By\).*~
						unusable_replacement = ~Not Usable By:~
						class_regexp = EVALUATE_BUFFER ~[%LNL%%MNL%%WNL%].*\bDruid.*~
						class_prefix = ~ ~
					END
				END
			END
		END
	END
	BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ // copies all item files
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
		READ_BYTE  0x21 class
		READ_BYTE  0x1f multi
		READ_BYTE  0x31 prof 
		PATCH_IF (prof = 95) BEGIN // scimitars
			WRITE_BYTE 0x21 (class BOR 0b01000000) // not usable by druids
			WRITE_BYTE 0x1f (multi BAND 0b00010000) // not usable by fighter/druids
		END
	END
	BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	COPY_EXISTING ~WEAPPROF.2da~ ~override~
		REPLACE_TEXTUALLY ~ ID ~ ~ TYPE ID ~
		// Druid
		SET_2DA_ENTRY_LATER ~#weapprof~ 17 10 0
		SET_2DA_ENTRY_LATER ~#weapprof~ 23 10 3
		SET_2DA_ENTRY_LATER ~#weapprof~ 28 10 3
		// Fighter/Druid
		SET_2DA_ENTRY_LATER ~#weapprof~ 17 19 0
		SET_2DA_ENTRY_LATER ~#weapprof~ 23 19 3
		SET_2DA_ENTRY_LATER ~#weapprof~ 28 19 3
		// Totemic Driud
		SET_2DA_ENTRY_LATER ~#weapprof~ 17 45 0
		SET_2DA_ENTRY_LATER ~#weapprof~ 23 45 3
		SET_2DA_ENTRY_LATER ~#weapprof~ 28 45 3
		// Shapeshifter
		SET_2DA_ENTRY_LATER ~#weapprof~ 17 46 0
		SET_2DA_ENTRY_LATER ~#weapprof~ 23 46 3
		SET_2DA_ENTRY_LATER ~#weapprof~ 28 46 3
		// Avenger
		SET_2DA_ENTRY_LATER ~#weapprof~ 17 47 0
		SET_2DA_ENTRY_LATER ~#weapprof~ 23 47 3
		SET_2DA_ENTRY_LATER ~#weapprof~ 28 47 3
		SET_2DA_ENTRIES_NOW ~#weapprof~ 1
		REPLACE_TEXTUALLY ~TYPE ~ ~      ~
END
//__________________________________________________________________________________
CLEAR_ARRAYS
